import org.gradle.accessors.dm.LibrariesForLibs

// Hack which exposes `libs` to this convention plugin
val libs = the<LibrariesForLibs>()

plugins {
    `java-library`
    kotlin("jvm")
    id("dev.triumphteam.root")
    id("com.github.hierynomus.license")
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnlyApi(libs.annotations)
}


java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(16))
    withSourcesJar()
    withJavadocJar()
}

kotlin {
    explicitApi()
}

license {
    header = rootProject.file("LICENSE")
    encoding = "UTF-8"
    useDefaultMappings = true

    include("**/*.kt")
    include("**/*.java")
}

tasks {
    withType<JavaCompile> {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }
}
